import fs = std.file;
import std.algorithm;
import std.exception;
import std.getopt;
import std.path;
import std.process;
import std.stdio;
import std.string;

string templateBaseDir;
void main(string[] args)
{
	templateBaseDir = dirName(args[0]);

	string codeTemplate = "vibe";

	auto options = args.getopt();
	if (options.helpWanted)
	{
		defaultGetoptPrinter("DORM package initializer", options.options);
		return;
	}

	if (args.length > 1)
		codeTemplate = args[1];

	runInit(codeTemplate);
}

void runInit(string codeTemplate)
{
	switch (codeTemplate)
	{
	case "base":
		copyTemplate("base");
		break;
	case "vibe":
		copyTemplate("base");
		copyTemplate("vibe");
		dubAdd("vibe-d");
		break;
	default:
		writeln("Unknown DORM init template: ", codeTemplate);
		writeln("Allowed template types:");
		writeln("- base");
		writeln("- vibe");
		return;
	}
	dubAdd("dorm:build-models");
	addGitIgnore();
	generateDatabaseTOML();
}

void copyTemplate(string name)
{
	auto srcDir = buildPath(templateBaseDir, "templates", name);
	if (!srcDir.endsWith(dirSeparator))
		srcDir ~= dirSeparator;
	auto dstDir = fs.getcwd;
	writeln("> cp -r ", srcDir, "* ", dstDir);

	foreach (file; fs.dirEntries(srcDir, fs.SpanMode.breadth))
	{
		if (file.baseName == ".gitkeep")
			continue;
		auto target = file.name;
		if (target.startsWith(srcDir))
			target = target[srcDir.length .. $];
		target = buildPath(dstDir, target);

		if (file.isDir)
		{
			if (!fs.exists(target))
				fs.mkdir(target);
		}
		else
		{
			fs.copy(file, target);
		}
	}
}

void dubAdd(string pkg)
{
	writeln("> dub add ", pkg);
	if (spawnProcess(["dub", "add", pkg]).wait != 0)
		stderr.writeln("Failed to add package ", pkg, "!");
}

void addGitIgnore()
{
	writeln("> adding .gitignore entries");
	auto path = buildPath(fs.getcwd, ".gitignore");
	auto file = File(path, "a");
	file.writeln("# DORM specific");
	file.writeln("# - auto generated by dorm:build-models sub-package as postBuildCommand (used by the auto-migrator)");
	file.writeln(".models.json");
	file.writeln("# - machine-specific database connection information");
	file.writeln("database.toml");
	file.writeln();
	file.writeln("*.sqlite3*");
	file.writeln();
}

void generateDatabaseTOML()
{
	version (Windows)
		auto rormcli = buildPath(templateBaseDir, "..", "rorm-cli.exe");
	else
		auto rormcli = buildPath(templateBaseDir, "..", "rorm-cli");

	auto output = buildPath(fs.getcwd, "database.toml");

	if (fs.exists(output))
		return;

	if (!fs.exists(rormcli))
		rormcli = "rorm-cli";

	writeln("> creating default database.toml");

	try
	{
		enforce(spawnProcess([rormcli, "migrate"]).wait == 0);
	}
	catch (Exception e)
	{
		stderr.writeln("WARNING: could not generate default database.toml - make sure rorm-cli is installed");
		stderr.writeln("         to generate a default database.toml, run one of these commands:");
		stderr.writeln();
		stderr.writeln("         $ dub run dorm -- migrate");
		stderr.writeln("         or");
		stderr.writeln("         $ rorm-cli migrate");
	}
}
